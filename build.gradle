buildscript {
	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.11.RELEASE'
	}

	repositories {
		maven { url 'https://repo.spring.io/libs-snapshot/' }
		maven { url 'https://repo.spring.io/plugins-snapshot/' }
	}
}

apply plugin: 'io.spring.convention.bom'
apply plugin: 'io.spring.convention.root'
apply plugin: 'io.spring.convention.springdependencymangement'

group = 'org.springframework.session'
description = 'Spring Session Maven Bill of Materials (BOM)'

configurations.archives.artifacts.clear()

artifacts {
	archives file('spring-session-bom.txt')
}

dependencyManagement {
	generatedPomCustomization {
		enabled = true
	}
}

install {
	repositories.mavenInstaller {
		pom.whenConfigured { pom ->
			customizePom(pom, project)
		}
	}
}

uploadArchives {
	repositories.mavenInstaller {
		pom.whenConfigured { pom ->
			customizePom(pom, project)
		}
	}
}

def customizePom(pom, project) {
	pom.project {
		packaging = 'pom'
		name = project.description
		description = project.description
		url = 'https://projects.spring.io/spring-session/'

		organization {
			name = 'Pivotal Software, Inc.'
			url = 'https://spring.io/'
		}

		licenses {
			license {
				name = 'Apache License, Version 2.0'
				url = 'https://www.apache.org/licenses/LICENSE-2.0'
				distribution = 'repo'
			}
		}

		developers {
			developer {
				id = 'rwinch'
				name = 'Rob Winch'
				email = 'rwinch@pivotal.io'
			}
		}

		scm {
			url = 'https://github.com/spring-projects/spring-session-bom'
			connection = 'scm:git:git@github.com/spring-projects/spring-session-bom.git'
			developerConnection = 'scm:git:git@github.com/spring-projects/spring-session-bom.git'
		}

		issueManagement {
			system = 'GitHub'
			url = 'https://github.com/spring-projects/spring-session/issues'
		}
	}
}

sonarqube.skipProject = true
mavenBom.enabled = false
