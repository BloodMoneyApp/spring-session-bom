import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.3.RELEASE'
	}

	repositories {
		maven { url 'https://repo.spring.io/libs-snapshot/' }
		maven { url 'https://repo.spring.io/plugins-snapshot/' }
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'io.spring.convention.artifactory'
apply plugin: 'io.spring.convention.root'

description = 'Spring Session Maven Bill of Materials (BOM)'
group = 'org.springframework.session'

configurations.archives.artifacts.clear()

def txt = file('spring-session-bom.txt')
def pom = file("$buildDir/poms/pom.xml")

artifacts {
	archives txt
	archives(pom) {
		type 'pom'
	}
}

task generatePom() {
	def srcDir = 'src'
	def pomsDir = "$buildDir/poms"

	inputs.files fileTree(srcDir)
	outputs.dir pomsDir

	doLast {
		copy {
			from srcDir
			into pomsDir
			filter(ReplaceTokens, tokens: [version: project.version])
		}
	}
}

install.dependsOn generatePom

sonarqube.skipProject = true
